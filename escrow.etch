persistent deposited_balance : UInt64;
persistent buyer  : Address;
persistent seller : Address;
persistent escrow : Address;
persistent start : UInt64;
persistent buyerOk : Bool;
persistent sellerOk: Bool;
persistent settledSinceBlock: UInt64;


@init
function constructor(owner: Address)
    use escrow;
    // Intentionally not setting values for remaining
    escrow.set(owner);
endfunction


@action
function initialise(_seller: Address, _buyer: Address)
    assert(!isInitialised(), "Contract has been already initialised.");
    authoriseTx();

    use deposited_balance;
    //use escrow;
    use buyer;
    use seller;
    use start;
    use buyerOk;
    use sellerOk;
    use settledSinceBlock;

    assert((_seller != null) && (_buyer != null), "Provided address of seller and/or buyer is null reference.");
    seller.set(_seller);
    buyer.set(_buyer);
    //NOTE: Suggested check
    //assert((buyer.get() != escrow.get()) && (seller.get() != escrow.get()) && (buyer.get() != seller.get()), "Owner, buyer and seller must be mutually different.");

    deposited_balance.set(0u64);
    start.set(getContext().block().blockNumber());
    buyerOk.set(false);
    sellerOk.set(false);
    settledSinceBlock.set(18446744073709551615u64);
endfunction


@action
function accept()
    verifyIsActive();

    use buyer;
    use seller;
    use start;
    use buyerOk;
    use sellerOk;

    var context = getContext();
    var tx: Transaction = context.transaction();
    var from = tx.from();

    if (from == buyer.get())
        buyerOk.set(true);
    else
        if (from == seller.get())
            sellerOk.set(true);
        endif
    endif

    if (buyerOk.get() && sellerOk.get())
        payBalance();
    else
        var releaseTimeout = 259200u64; // 259200[block/30xDay] = 30[Day]*24[h/Day]*60[m/h]*60[s/m] / 10[s/block]
        var expirationBlock = start.get() + releaseTimeout;

        // Freeze 30 days before release to buyer. The customer has to remember to call this method after freeze period.
        if (buyerOk.get() && (!sellerOk.get()) && (context.block().blockNumber() > expirationBlock))
            selfdestruct();
        endif
    endif
endfunction


@action
function deposit()
    verifyIsActive();

    use deposited_balance;
    use buyer;

    var context = getContext();
    var tx: Transaction = context.transaction();
    var transfers: Array<Transfer> = tx.transfers();

    assert(transfers.count() == 1i32, "There must be 1 native transfers defined in the transaction.");
    assert(transfers[0].to() == tx.contractAddress(), "Transfer destination address must be contract address.");
    assert(tx.from() == buyer.get(), "Deposit must be done from buyer address.");

    deposited_balance.set(deposited_balance.get() + transfers[0].amount());
endfunction


@action
function cancel()
    verifyIsActive();

    use buyer;
    use seller;
    use buyerOk;
    use sellerOk;

    var context = getContext();
    var tx: Transaction = context.transaction();
    var from = tx.from();

    if (from == buyer.get())
        buyerOk.set(false);
    else
        if (from == seller.get())
            sellerOk.set(false);
        endif
    endif

    // if both buyer and seller would like to cancel, money is returned to buyer
    if ((!buyerOk.get()) && (!sellerOk.get()))
        selfdestruct();
    endif
endfunction


@action
function kill()
    authoriseTx();
    verifyIsActive();

    selfdestruct();
endfunction


@action
function withdrawExcessBalance()
    use deposited_balance;
    use escrow;

    authoriseTx();

    var contract_balance = balance();
    var bonded_balance = deposited_balance.get();
    assert(contract_balance >= bonded_balance, "INCONSISTENCY: Insufficient contract deposited_balance.");

    transfer(escrow.get(), contract_balance - bonded_balance);
endfunction


@query
function deposited_balance() : UInt64
    use deposited_balance;
    return deposited_balance.get();
endfunction


@query
function status() : StructuredData
    use deposited_balance;
    use buyer;
    use seller;
    use escrow;
    use start;
    use buyerOk;
    use sellerOk;
    use settledSinceBlock;

    var status = StructuredData();
    status.set("escrow", escrow.get());
    if (isInitialised())
        status.set("deposited_balance", deposited_balance.get());
        status.set("buyer", buyer.get());
        status.set("seller", seller.get());
        status.set("start", start.get());
        status.set("buyerOk", toString(buyerOk.get()));
        status.set("sellerOk", toString(sellerOk.get()));
        status.set("settledSinceBlock", settledSinceBlock.get());
    endif

    return status;
endfunction


// ******   Private functions   *************************

function payBalance()
    // we are sending ourselves (contract creator) a fee
    use deposited_balance;
    use seller;
    use escrow;

    var fee = deposited_balance.get() / 100u64;
    var amountPayable = deposited_balance.get() - fee;

    transfer(escrow.get(), fee);
    transfer(seller.get(), amountPayable);
    deposited_balance.set(0u64);

    selfdestruct();
endfunction

function selfdestruct()
    use deposited_balance;
    use buyer;
    use settledSinceBlock;

    if (deposited_balance.get() > 0u64)
        transfer(buyer.get(), deposited_balance.get());
        deposited_balance.set(0u64);
    endif

    settledSinceBlock.set(getContext().block().blockNumber());
endfunction

function isInitialised() : Bool
    use seller;
    return (seller.get(null) != null);
endfunction

function isActive() : Bool
    use settledSinceBlock;
    return (getContext().block().blockNumber() < settledSinceBlock.get());
endfunction

function verifyIsActive()
    assert(isInitialised(), "Contract has not been initialised yet.");
    assert(isActive(), "Contract has been settled and is no more active.");
endfunction

function authoriseTx() : Transaction
    use escrow;
    var tx: Transaction = getContext().transaction();
    assert(tx.from() == escrow.get(), "Tx sender must be owner address of the contract.");
    return tx;
endfunction
